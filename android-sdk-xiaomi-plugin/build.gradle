apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Add SDK via module.
    compileOnly project(':android-sdk')
    // Add SDK via Maven.
    // implementation 'io.adtrace:android-sdk:2.4.1'

    // Add xiaomi referrer lib via Maven.
    implementation 'com.miui.referrer:homereferrer:1.0.0.6'
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

task adtraceXiaomiAndroidAar (type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-plugin-xiaomi-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-plugin-xiaomi-release.aar', "${project.name}.aar"

}

task adtraceXiaomiAndroidJar(type: Jar) {
    dependsOn 'packageReleaseAssets'
    dependsOn 'compileReleaseJavaWithJavac'

    from('build/intermediates/library_assets/release/packageReleaseAssets/out/') {
        into('assets')
    }
    from('build/intermediates/javac/release/classes/')

    archiveName "${project.name}.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task adtraceXiaomiAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adtraceXiaomiAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adtraceXiaomiAndroidJar
    archives adtraceXiaomiAndroidJavadocsJar
    archives adtraceXiaomiAndroidSourcesJar
}
publishing {
    publications {
        mavenAndroidXiaomi(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.adtraceGroupId
            artifactId 'android-sdk-plugin-xiaomi'
            version rootProject.ext.coreVersionName

            artifact adtraceXiaomiAndroidJar
            artifact adtraceXiaomiAndroidJavadocsJar
            artifact adtraceXiaomiAndroidSourcesJar
        }
    }

    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The AdTrace SDK for Android'
            name 'AdTrace Android SDK'
            url 'https://github.com/adtrace/adtrace_sdk_android'
            organization {
                name 'adtrace'
                url 'https://www.adtrace.io'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adtrace/adtrace_sdk_android.git'
                connection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
                developerConnection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
            }
            developers {
                developer {
                    name 'Nasser Amini'
                    email 'namini40@gmail.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidXiaomiPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidXiaomiPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidXiaomiPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenAndroidXiaomi
}