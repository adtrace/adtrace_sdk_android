apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/staging/"
    }
}

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
        }
    }

    defaultConfig {
        minSdkVersion rootProject.ext.webbridgeMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
    }
    namespace 'io.adtrace.sdk.webbridge'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Add SDK via module.
    compileOnly project(':android-sdk')
    // Add SDK via Maven.
    // implementation 'io.adtrace:android-sdk:2.5.0'
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

task adtraceWebBridgeAndroidAar (type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'android-sdk-plugin-webbridge-release.aar'
    destinationDir file('build/libs/')
    rename 'android-sdk-plugin-webbridge-release.aar', "${project.name}.aar"
}

task adtraceWebBridgeAndroidJar(type: Jar) {
    dependsOn 'packageReleaseAssets'
    dependsOn 'compileReleaseJavaWithJavac'

    from('build/intermediates/library_assets/release/out/') {
        into('assets')
    }
    from('build/intermediates/javac/release/classes/')

    archiveFileName = project.provider { "${project.name}.jar" }
}

task adtraceWebBridgeAndroidSourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adtraceWebBridgeAndroidJar
    archives adtraceWebBridgeAndroidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
//        mavenAndroidWebBridgeJar(MavenPublication) {
//            customizePomForJar(pom)
//            groupId rootProject.ext.adtraceGroupId
//            artifactId 'adtrace-android-webbridge'
//            version rootProject.ext.coreVersionName
//
//            artifact adtraceWebBridgeAndroidJar
//            artifact adtraceWebBridgeAndroidJavadocsJar
//            artifact adtraceWebBridgeAndroidSourcesJar
//        }

            mavenAndroidWebBridgeAar(MavenPublication) {
                customizePomForAar(pom)
                groupId rootProject.ext.adtraceGroupId
                artifactId rootProject.ext.webbridgeSdkArtifactId
                version rootProject.ext.coreVersionName

                artifact("$buildDir/libs/android-sdk-plugin-webbridge.aar") {
                    builtBy tasks.getByPath("adtraceWebBridgeAndroidAar")
                }
                artifact adtraceWebBridgeAndroidSourcesJar
            }
        }

        repositories {
            maven {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                if (project.hasProperty("sonatypeUsername")) {
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
    }
    signing {
//        sign publishing.publications.mavenAndroidWebBridgeJar
        sign publishing.publications.mavenAndroidWebBridgeAar
    }
}

def customizePomForJar(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The WebBridge plugin for AdTrace SDK Android'
            name 'AdTrace Android SDK WebBridge plugin'
            url 'https://github.com/adtrace/adtrace_sdk_android'

            organization {
                name 'adtrace'
                url 'https://www.adtrace.io'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adtrace/adtrace_sdk_android.git'
                connection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
                developerConnection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
            }
            developers {
                developer {
                    name 'Nasser Amini'
                    email 'namini40@gmail.com'
                }
            }
        }
    }
}

def customizePomForAar(pom) {
    pom.withXml {
        // Remove the existing packaging field, as it has to be overridden with type aar
        asNode().remove(asNode().get("packaging"))

        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The WebBridge plugin for AdTrace SDK Android'
            name 'AdTrace Android SDK WebBridge plugin'
            url 'https://github.com/adtrace/adtrace_sdk_android'
            packaging 'aar'
            organization {
                name 'adtrace'
                url 'https://www.adtrace.io'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adtrace/adtrace_sdk_android.git'
                connection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
                developerConnection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
            }
            developers {
                developer {
                    name 'Nasser Amini'
                    email 'namini40@gmail.com'
                }
            }
        }
    }
}

model {
//    tasks.generatePomFileForMavenAndroidWebBridgeJarPublication {
//        destination = file("${project.buildDir}/generated-pom.xml")
//    }
//    tasks.publishMavenAndroidWebBridgePublicationToMavenLocal {
//        dependsOn project.tasks.signArchives
//    }
    /* TODO check if/how to replace this tasks
tasks.publishMavenAndroidWebBridgePublicationToMavenRepository {
    dependsOn project.tasks.signArchives
}
*/
    tasks.generatePomFileForMavenAndroidWebBridgeAarPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
}


