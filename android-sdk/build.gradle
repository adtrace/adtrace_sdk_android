apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
        consumerProguardFiles 'adtrace-proguard-rules.txt'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task adtraceCoreAarRelease(type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-release.aar', 'android-sdk-release.aar'
}

task adtraceCoreAarDebug(type: Copy) {
    dependsOn 'assembleDebug'

    from('build/outputs/aar/')
    include 'sdk-debug.aar'
    destinationDir file('build/libs/')
    rename 'sdk-debug.aar', 'android-sdk-debug.aar'
}

task adtraceCoreJarRelease(type: Jar) {
    dependsOn 'compileReleaseJavaWithJavac'
    from('build/intermediates/javac/release/classes/')
    archiveName "android-sdk-release.jar"
}

task adtraceCoreJarDebug(type: Jar) {
    dependsOn 'compileDebugJavaWithJavac'
    from('build/intermediates/javac/debug/classes/')
    archiveName "android-sdk-debug.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation.
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task adtraceCoreAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adtraceCoreAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adtraceCoreJarRelease
    archives adtraceCoreAndroidJavadocsJar
    archives adtraceCoreAndroidSourcesJar
}

publishing {
    publications {
        mavenAndroidCore(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.adtraceGroupId
            artifactId 'android-sdk'
            version rootProject.ext.coreVersionName

            artifact adtraceCoreJarRelease
            artifact adtraceCoreAndroidJavadocsJar
            artifact adtraceCoreAndroidSourcesJar
        }

        mavenAndroidCoreAAR(MavenPublication) {
            customizePomForAar(pom)
            groupId rootProject.ext.adtraceGroupId
            artifactId 'android-sdk'
            version rootProject.ext.coreVersionName

            artifact("$buildDir/outputs/aar/android-sdk-release.aar"){
                builtBy tasks.getByPath("adtraceCoreAarRelease")
            }
            artifact adtraceCoreAndroidJavadocsJar
            artifact adtraceCoreAndroidSourcesJar
        }

    }

    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The AdTrace SDK for Android'
            name 'AdTrace Android SDK'
            url 'https://github.com/adtrace/adtrace_sdk_android'
            organization {
                name 'adtrace'
                url 'https://www.adtrace.io'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adtrace/adtrace_sdk_android.git'
                connection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
                developerConnection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
            }
            developers {
                developer {
                    name 'Nasser Amini'
                    email 'namini40@gmail.com'
                }
            }
        }
    }
}

def customizePomForAar(pom) {
    pom.withXml {
        // Remove the existing packaging field, as it has to be overridden with type aar
        asNode().remove(asNode().get("packaging"))

        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The AdTrace SDK for Android'
            name 'AdTrace Android SDK'
            url 'https://github.com/adtrace/adtrace_sdk_android'
            packaging 'aar'
            organization {
                name 'adtrace'
                url 'https://www.adtrace.io'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adtrace/adtrace_sdk_android.git'
                connection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
                developerConnection 'scm:git:git@github.com:adtrace/adtrace_sdk_android.git'
            }
            developers {
                developer {
                    name 'Nasser Amini'
                    email 'namini40@gmail.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidCorePublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidCorePublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidCorePublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
    tasks.generatePomFileForMavenAndroidCoreAARPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidCoreAARPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidCoreAARPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenAndroidCore
    sign publishing.publications.mavenAndroidCoreAAR
}
